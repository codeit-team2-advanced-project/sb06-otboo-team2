plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.10'
    id 'io.spring.dependency-management' version '1.1.7'
    id "org.sonarqube" version "7.1.0.6387"
    id 'jacoco'
}
ext {
    springCloudVersion = "2025.0.1"
}

group = 'codeit.sb06'
version = '0.0.1-SNAPSHOT'
description = 'otboo'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

sonar {
    properties {
        property "sonar.projectKey", "codeit-team2-advanced-project_sb06-otboo-team2"
        property "sonar.organization", "codeit-team2-advanced-project"
        property "sonar.coverage.jacoco.xmlReportPaths", "build/reports/jacoco/test/jacocoTestReport.xml"

        // SonarQube 분석에서 제외할 파일 및 디렉토리 설정
        property "sonar.exclusions", [
                "**/*Application.java",
                "**/dto/**",
                "**/entity/**",
                "**/config/**",
                "**/Q*.java",                       // Querydsl 생성 파일
                "**/*Request.java",
                "**/*Response.java",
                "**/*Exception.java",
                "**/enums/**",
                "**/*Event.java",
        ].join(",")
    }
}

jacoco {
    toolVersion = "0.8.14"
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-mail'
    implementation 'org.springframework.boot:spring-boot-starter-websocket'
    implementation 'org.springframework.boot:spring-boot-starter-batch'
//    implementation 'org.springframework.boot:spring-boot-starter-oauth2-authorization-server'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

    implementation 'io.github.openfeign.querydsl:querydsl-core:7.0'
    implementation 'io.github.openfeign.querydsl:querydsl-jpa:7.0'
    annotationProcessor 'io.github.openfeign.querydsl:querydsl-apt:7.0'
    annotationProcessor 'jakarta.persistence:jakarta.persistence-api'
    annotationProcessor 'jakarta.annotation:jakarta.annotation-api'

    implementation 'org.springframework.boot:spring-boot-starter-validation'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'
    implementation 'com.nimbusds:nimbus-jose-jwt:10.3'
//    implementation 'org.springframework.boot:spring-boot-starter-data-redis'

    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'io.micrometer:micrometer-registry-prometheus'

    testImplementation 'io.github.dvgaba:easy-random-core:7.1.1'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.batch:spring-batch-test'
    testImplementation 'org.springframework.security:spring-security-test'
    runtimeOnly 'org.postgresql:postgresql'
    runtimeOnly 'com.h2database:h2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.withType(JavaCompile).configureEach {
    options.annotationProcessorPath = configurations.annotationProcessor
    options.compilerArgs += [
            '-processor',
            'com.querydsl.apt.jpa.JPAAnnotationProcessor,lombok.launch.AnnotationProcessorHider$AnnotationProcessor'
    ]
}

test {
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
    }
}
dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:$springCloudVersion"
    }
}
